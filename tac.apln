:NameSpace Tac

∇ expr←explain train
 :With ⎕NS''
     p∆f←'+-×÷⌈⌊*⍟|!○~∨∧⍱⍲<≤=≥>≠≡≢⍴,⍪⍳↑↓?⍒⍋⍉⌽⊖∊⊥⊤⍎⍕⌹⊂⊃∪∩⍷⌷⊆⊢⊣⍸'
     s∆f←'/⌿\⍀'
     m∆o←'/⌿\⍀¨&⌶⌸'
     d∆o←'.@∘⍛⍣⍥⍠⍤⌺'

     Name←(' ',,∘' '),∘(⍕⎕UCS)

     (p∆n s∆n m∆n d∆n)←'∆∆⍙⍙'Name¨¨p∆f s∆f m∆o d∆o

     ∆ZLIDE←'⍬'⍨
     ∆RANK  ← {⍺←⊢ ⋄ f←⍺ ⋄ c ← (,'⊢')≢⎕cr'f' ⋄ Pad (⍕⍺),(Pad(Dfn Drop(c⍴'⍺')⍺⍺'⍵'),'⍤',⍕⍵⍵),⍵}
     ∆OUTER ← {⍺←⊢ ⋄ Pad (⍕⍺),(⍺⍺,'.',(Dfn Drop '⍺' ⍵⍵ '⍵')),⍵}

     Drop←{0=×/⍴r←⊃↓/1 ¯1,⊂⍵:⍵ ⋄ r}

     _Lit←⍠'Regex' 0
     Zilde←('⍬'⎕R(' ∆ZLIDE ')_Lit) ⍝ ⍬
     Slash←((,d∆o∘.,s∆f)⎕R(,d∆o∘.,s∆n)_Lit) ⍝ @/
     Rank←(('⍤',¨⎕D)⎕R(' ∆RANK '∘,¨⎕D)_Lit) ⍝ ⍤X
     Outer ← ('∘.'⎕R(' ''∘'' ∆OUTER ')_Lit) ⍝ ∘.


     _Assign_←{⍎⍺,⍺⍺,⍵,⍵⍵}
     Pad←'(',,∘')'
     Dfn←'{',,∘'}'
     (p∆n,s∆n)('←{⍺←{⍵ ⋄ ⍺⍺} ⋄ (Pad⍣(⍬≢⍵)) (⍕⍺),'''_Assign_''',(⍕⍵) }')¨p∆f,s∆f

     m∆n('←{⍺←⊢ ⋄ Pad (⍕⍺),(⍕⍺⍺,⍬),'''_Assign_''',        (⍕⍵)}')¨m∆o
     d∆n('←{⍺←⊢ ⋄ Pad (⍕⍺),(⍕⍺⍺,⍬),'''_Assign_''',(⍕⍵⍵,⍬),(⍕⍵)}')¨d∆o


     ⍙168 ←{⍺←⊢ ⋄ Pad ⍺,('¨',⍨Dfn Drop'⍺'(⍺⍺)'⍵'),⍵}
     ⍙9016←{⍺←⊢ ⋄ Pad ⍺,('⌸',⍨Dfn Drop'⍺'(⍺⍺)'⍵'),⍵}

     ∆8866←{⍺←⍵ ⋄ ⍵≡⍬:(⍕⍺),'⊢',(⍕⍵) ⋄ ⍺⊢⍥⍕⍵}
     ∆8867←{⍺←⍵ ⋄ ⍵≡⍬:(⍕⍺),'⊣',(⍕⍵) ⋄ ⍺⊣⍥⍕⍵}

     ⍙9051←{⍺←⍵ ⋄ ⍵≡⍬:(⍕⍺⍺,⍬),'⍛',(⍕⍵⍵,⍬) ⋄ ⍵ ⍵⍵⍨⍺⍺ ⍺}
     ⍙9064←{⍺←⍵ ⋄ ⍵≡⍬:(⍕⍺⍺,⍬),'⍨'         ⋄ ⍵(⍕⍺⍺⊣⊢)⍺}
     ⍙9061←{⍺←⊢ ⋄ ⍵≡⍬:(⍕⍺⍺,⍬),'⍥',(⍕⍵⍵,⍬) ⋄ ⍺(⍺⍺⍥⍵⍵)⍵}
     ⍙8728←{⍺←⊢ ⋄ ⍵≡⍬:(⍕⍺⍺,⍬),'∘',(⍕⍵⍵,⍬) ⋄ ⍺(⍺⍺∘⍵⍵)⍵}
     ⍙9060←{⍺←⊢ ⋄ ⍵≡⍬:(⍕⍺⍺,⍬),'⍤',(⍕⍵⍵,⍬) ⋄ ⍺(⍺⍺⍤⍵⍵)⍵}

     train ← (('''[^'']+''',⍥⊆' *\'∘,¨p∆f,m∆o,d∆o)⎕R('&',p∆n,m∆n,d∆n))Outer Slash Zilde Rank train
     f ← {⊂Dfn Drop⍵}
     expr ← (f(⍎train)'⍵'),⍥⊆{0::'N/A' ⋄ f'⍺'(⍎train)'⍵'}''
 :EndWith
 ∇

 :EndNameSpace